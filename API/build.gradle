apply plugin: 'maven-publish'

version = '1.0.7'
archivesBaseName = 'Hydrion-API'

java {
    withSourcesJar()
    withJavadocJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                groupId = project.group
                artifactId = 'hydrion-api'
                version = project.version
                name = 'HydrionAPI'
                description = 'The API used to create extensions for Hydrion'
                url = 'https://github.com/Hyriode/Hydrion'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
            name = "Hydrion_maven"
            url = "https://maven.pkg.github.com/Hyriode/Hydrion"
        }
    }
}

dependencies {
    api 'fr.hyriode:hyriapi:1.4.3'
    api 'io.netty:netty-all:4.1.87.Final'
}