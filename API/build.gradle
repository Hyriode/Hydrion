plugins {
    id "com.github.johnrengelman.shadow"
}

apply plugin: 'maven-publish'

version = '1.0.0'
archivesBaseName = 'Hydrion-API'

java {
    withSourcesJar()
    withJavadocJar()

    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}

shadowJar {
    configurations = [project.configurations.runtimeClasspath]
    classifier ''
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                groupId = project.group
                artifactId = 'hydrion-api'
                version = project.version
                name = 'HydrionAPI'
                description = 'The API used to create extension and modules for Hydrion'
                url = 'https://github.com/Hyriode/Hydrion'
            }
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv('USERNAME')
                password System.getenv('TOKEN')
            }
            name = "Hydrion_maven"
            url = "https://maven.pkg.github.com/Hyriode/Hydrion"
        }
    }
}

dependencies {
    api 'io.netty:netty-all:4.1.75.Final'
    api 'com.google.guava:guava:31.1-jre'
    api 'org.mongodb:mongodb-driver-reactivestreams:4.5.1'
}